WHITESPACE     = _{ " " | "\n" | "\t" }
pdf            =  { PDF? }
PDF            =  { "pdf" }
hour_minute    = ${ number ~ ":" ~ number | number ~ ^"h" ~ number ~ ^"m"? }
number         = @{ ASCII_DIGIT+ }
year           = @{ ASCII_DIGIT{4} }
day            = @{ ASCII_DIGIT{1,2} }
date_sep       = _{ "/" | "-" | WHITESPACE+ }
year_month     = ${ year ~ date_sep ~ month }
month_year     = ${ month ~ date_sep ~ year }
year_month_day = ${ year ~ date_sep ~ month ~ date_sep ~ day }
month_day      = ${ month ~ date_sep ~ day }
time_zone      = @{ (LETTER | "/" | "_")+ }
name           = @{ (LETTER | "-" | "'")+ }
bool           =  { TRUE | FALSE }
targets        =  { target* }
target         = ${ "@" ~ (target_index | TARGET_ALL | TARGET_ME) }
target_index   = ${ number }
word           = @{ (LETTER | "-")+ }
date_hint      =  { year_month_day | month_day | weekday | day }
month          = _{
    MONTH_01 |
    MONTH_02 |
    MONTH_03 |
    MONTH_04 |
    MONTH_05 |
    MONTH_06 |
    MONTH_07 |
    MONTH_08 |
    MONTH_09 |
    MONTH_10 |
    MONTH_11 |
    MONTH_12
}
weekday = {
    WEEKDAY_0 |
    WEEKDAY_1 |
    WEEKDAY_2 |
    WEEKDAY_3 |
    WEEKDAY_4 |
    WEEKDAY_5 |
    WEEKDAY_6
}

command = {
    SOI ~ (
        command_help              |
        command_persons           |
        command_new_person        |
        command_person_admin      |
        command_set_time_zone     |
        command_set_language      |
        command_clear_date        |
        command_clear             |
        command_span              |
        command_span_date         |
        command_span_date_date    |
        command_enter_hour_minute |
        command_enter             |
        command_leave_hour_minute |
        command_leave             |
        command_month_year_month  |
        command_month_month       |
        command_month
    ) ~ EOI
}

command_help              = { HELP }
command_persons           = { PERSONS }
command_person_admin      = { PERSON ~ target ~ ADMIN ~ bool }
command_new_person        = { PERSON ~ NEW ~ name+ }
command_set_time_zone     = { SET ~ TIME_ZONE ~ time_zone }
command_set_language      = { SET ~ LANGUAGE ~ word }
command_clear             = { CLEAR }
command_clear_date        = { CLEAR ~ date_hint }
command_span              = { ENTER? ~ hour_minute ~ LEAVE? ~ hour_minute }
command_span_date         = { ENTER? ~ date_hint ~ hour_minute ~ LEAVE? ~ hour_minute }
command_span_date_date    = { ENTER? ~ date_hint ~ hour_minute ~ LEAVE? ~ date_hint ~ hour_minute }
command_enter             = { ENTER }
command_enter_hour_minute = { ENTER ~ hour_minute }
command_leave             = { LEAVE }
command_leave_hour_minute = { LEAVE ~ hour_minute }
command_month             = { MONTH ~ pdf? }
command_month_month       = { MONTH? ~ month ~ pdf? }
command_month_year_month  = { MONTH? ~ (year_month | month_year) ~ pdf? }
