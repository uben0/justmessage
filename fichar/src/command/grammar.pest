WHITESPACE   = _{ " " }
hour_minute  = ${ number ~ ":" ~ number | number ~ ^"h" ~ number ~ ^"m"? }
number       = @{ ASCII_DIGIT+ }
year         = @{ ASCII_DIGIT{4} }
year_month   = ${ year ~ ("/" | "-" | WHITESPACE+ ) ~ month_n }
month_year   = ${ month_n ~ ("/" | "-" | WHITESPACE+ ) ~ year }
time_zone    = @{ (LETTER | "/" | "_")+ }
name         = @{ (LETTER | "-" | "'")+ }
bool         =  { bool_true | bool_false }
targets      =  { target* }
target       = ${ "@" ~ (target_index | target_all | target_me) }
target_index = ${ number }
month_n      = _{
    month_01 |
    month_02 |
    month_03 |
    month_04 |
    month_05 |
    month_06 |
    month_07 |
    month_08 |
    month_09 |
    month_10 |
    month_11 |
    month_12
}
word = @{ (LETTER | "-")+ }

command = {
    SOI ~ (
        command_help              |
        command_persons           |
        command_new_person        |
        command_person_admin      |
        command_set_time_zone     |
        command_set_language      |
        command_span              |
        command_enter_hour_minute |
        command_enter             |
        command_leave_hour_minute |
        command_leave             |
        command_month_year_month  |
        command_month_month       |
        command_month
    ) ~ EOI
}

command_help              = { help }
command_persons           = { persons }
command_person_admin      = { person ~ target ~ admin ~ bool }
command_new_person        = { person ~ new ~ name+ }
command_set_time_zone     = { set ~ TIME_ZONE ~ time_zone }
command_set_language      = { set ~ ^"language" ~ word }
command_span              = { hour_minute ~ hour_minute }
command_enter             = { enter }
command_enter_hour_minute = { enter ~ hour_minute }
command_leave             = { leave }
command_leave_hour_minute = { leave ~ hour_minute }
command_month             = { month }
command_month_month       = { month? ~ month_n }
command_month_year_month  = { month? ~ (year_month | month_year) }
