WHITESPACE   = _{ " " }
enter        = _{ ^"enter" }
leave        = _{ ^"leave" }
month        = _{ ^"month" }
month_n      = _{ month_july | month_august }
month_july   = @{ "7" | "07" | ^"jully" | ^"jul" }
month_august = @{ "8" | "08" | ^"august" | ^"aug" }
hour_minute  = ${ number ~ ":" ~ number | number ~ ^"h" ~ number ~ ^"m"? }
number       = @{ ASCII_DIGIT+ }
year         = @{ ASCII_DIGIT{4} }
year_month   = ${ year ~ ("/" | "-" | WHITESPACE+ ) ~ month_n }
month_year   = ${ month_n ~ ("/" | "-" | WHITESPACE+ ) ~ year }
time_zone    = @{ (LETTER | "/" | "_")+ }
name         = @{ (LETTER | "-" | "'")+ }
bool         =  { bool_true | bool_false }
targets      =  { target* }
target       = ${ "@" ~ (target_index | target_all | target_me) }
target_index = ${ number }
target_all   = ${ ^"all"}
target_me    = ${ ^"me"}

command = {
    SOI ~ (
        command_help              |
        command_persons           |
        command_new_person        |
        command_person_admin      |
        command_set_time_zone     |
        command_span              |
        command_enter_hour_minute |
        command_enter             |
        command_leave_hour_minute |
        command_leave             |
        command_month_year_month  |
        command_month_month       |
        command_month
    ) ~ EOI
}

command_help              = { ^"help" }
command_persons           = { ^"persons" }
command_person_admin      = { ^"person" ~ target ~ ^"admin" ~ bool }
command_new_person        = { ^"person" ~ ^"new" ~ name+ }
command_set_time_zone     = { ^"set" ~ ^"time" ~ ^"zone" ~ time_zone }
command_span              = { hour_minute ~ hour_minute }
command_enter             = { enter }
command_enter_hour_minute = { enter ~ hour_minute }
command_leave             = { leave }
command_leave_hour_minute = { leave ~ hour_minute }
command_month             = { month ~ targets }
command_month_month       = { month? ~ month_n ~ targets }
command_month_year_month  = { month? ~ (year_month | month_year) ~ targets }
